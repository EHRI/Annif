PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
CONSTRUCT {
  ?c a skos:Concept .
  ?c skos:prefLabel ?prefLabel .
  ?c skos:altLabel ?altLabel .
  ?c ?rel ?c2 .
  ?coll a skos:Collection .
  ?coll skos:member ?c .
}
WHERE {
  ?c a skos:Concept .
  <http://www.yso.fi/onto/yso/p26593> skos:member ?c .
  ?c skos:prefLabel ?prefLabel .
  OPTIONAL { ?c skos:altLabel ?altLabel }
  OPTIONAL {
    VALUES ?rel { skos:broader skos:narrower skos:related } 
    ?c ?rel ?c2 .
    <http://www.yso.fi/onto/yso/p26593> skos:member ?c2 .   
  }
  OPTIONAL { ?coll skos:member ?c . 
    # don't include the Archaeology collection as it contains all concepts
    FILTER (?coll != <http://www.yso.fi/onto/yso/p26593>)
  }
} 
